# ██████╗ ██████╗ ███╗   ██╗████████╗██████╗  █████╗ ████████╗ ██████╗ ███████╗
#██╔════╝██╔═══██╗████╗  ██║╚══██╔══╝██╔══██╗██╔══██╗╚══██╔══╝██╔═══██╗██╔════╝
#██║     ██║   ██║██╔██╗ ██║   ██║   ██████╔╝███████║   ██║   ██║   ██║███████╗
#██║     ██║   ██║██║╚██╗██║   ██║   ██╔══██╗██╔══██║   ██║   ██║   ██║╚════██║
#╚██████╗╚██████╔╝██║ ╚████║   ██║   ██║  ██║██║  ██║   ██║   ╚██████╔╝███████║
# ╚═════╝ ╚═════╝ ╚═╝  ╚═══╝   ╚═╝   ╚═╝  ╚═╝╚═╝  ╚═╝   ╚═╝    ╚═════╝ ╚══════╝
#==============================================================================        
# Por Sieg dev                                                                      
# Esse skript tem as seguintes funcoes:
# criar documentos que pode ser pego com /documento
# criar contratos usando o /contrato
# promover pessoas com /promover
# demitir pessoas com /demitir

#======================== Creditos a Sieg/fek_/Felipe
# https://github.com/feliperobertosouz
# Codigo autoral, deixe os devivos creditos!
# Codigo aberto, disponivel em!: https://github.com/feliperobertosouz/publicSkripts
# Discord: Sieg#9659

# ============================== ! IMPORTANTE ! ====================
# Para esse skript funcionar ele depende muito se as permissoes dos grupos e nome dos grupos estao corretamente informados
# Se voce for criar por exemplo a profissao de fazendeiros, tendo desde fazendeiro1 ate fazendeiro4 tenha certeza de
# Todos os grupos terem a permissao fazendeiro e etc
# O fazendeiro4 ter as permissoes contrato,demitir,promover
# O GRUPO CHEFE OU SEJA fazendeiro4 deve ter uma permissao fazendeiro4 ou a permissao de que determina que ela é chefe

# Exemplo, os grupos alquimista1,alquimista2,alquimista3 devem ter a permissao alquimista
# e o grupo alquimista4 que é o chefe deve ter as permissoes alquimista4,promover,demitir,contrato

# OUTRO AVISO
# Se o codigo estiver com problemas falar comigo por favor! eu posso arrumar para voce e fazer o possivel
# A partir do ponto que voce mexer nas linhas depois de permissoes eu nao me responsabilizo por erros

#============ PERMISSOES
# Na funcao seguinte esta o codigo para checar se o player é um chefe de profissao e entao responder qual a profissao
# Se a pessoa for fazendeiro4 entao essa funcao devolve fazendeiro
# Para adicionar novas profissoes apenas adicionar mais uma linha de else if seguindo o padrao a baixo
# LEMBRANDO QUE ELA SERVE PARA PEGAR 1 PERMISSAO! SE O PLAYER DE ALGUMA FORMA FOR CHEFE DE MAIS DE 1 PROFISSAO ELA N VAI SERVIR
function Chefe(p: player) :: string:
    if {_p} has permission "chefe.cartorio":
        return "cartorio"
    else if {_p} has permission "chefe.fazendeiro":
        return "fazendeiro"
    else if {_p} has permission "chefe.ferreiro":
        return "ferreiro"
    else if {_p} has permission "chefe.alquimista":
        return "alquimista"
    else if {_p} has permission "chefe.cacador":
        return "cacador"
    else if {_p} has permission "chefe.cassino":
        return "cassino"
    else if {_p} has permission "chefe.construtor":
        return "construtor"
    else:
        return "indefinido"


# ========== FUNCOES 
function Contrato(p: player, g: string, action: string):
    set {_command} to "lp user %{_p}% group %{_action}% %{_g}%"
    make console execute command {_command}

function Permission(p: player, g: string, action: string):
    if {_action} is "true":
        set {_command} to "lp user %{_p}% permission set %{_g}% %{_action}%"
        make console execute command {_command}
    else if {_action} is "false":
        set {_command} to "lp user %{_p}% permission unset %{_g}%"
        make console execute command {_command}

# VARIAVEIS
# trabalhadores serve como trava para que um player nao possa assinar mais de 1 contrato por vez
variables:
    {trabalhadores::1::*} = 0
    

on load:
    #documento em branco usado para fazer contratos e afins
    set {Documento} to flower banner pattern named "&5Documento em &f Branco" with lore "&fUm documento usado para fazer documentos"
    add nbt compound of "{CustomModelData:1,oficial:1}" to nbt compound of {Documento}
    set {message} to "&l[&6Contratos&f&l]:&f"


# ============ /documento
# Esse comando so pode ser usado por admins, ele entrega para o player que executa o documento em branco
command /documento:
    permission: admin
    trigger:
        give the player 1 of {Documento}


# =============================== /contrato
#O /contrato serve para criar um contrato estando com um documento em branco em maos, ele age de 2 formas dependendo de seu cargo
#permissao: contrato/op
# Player comum:
# Voce deve ser um chefe de profissao e ter a permissao especifica de contrato
# Voce sendo um player comum deve apenas dar /contrato com o documento em maos e um contrato de sua profissao1 ira surgir na sua mao
#Admin
# Voce deve ter o documento em branco em maos
# uma vez que voce use /contrato voce pode inserir um texto, o texto inserido ira criar um contrato referente ao que foi escrito
# Exemplo "/contrato werewolf" criaria um contrato que adiciona o player que usar no grupo werewolf
#Exemplos:
# /contrato
# /contrato werewolf
#======================================= CONTRATO
command /Contrato [<text>]:
    permission: contrato
    trigger:
        set {_nbt} to nbt compound from player's held item
        set {_tag} to tag "oficial" of {_nbt}
        set {_model} to tag "CustomModelData" of {_nbt}
        if player has permission "admin":
            set {_permissao} to arg-1
            if {_permissao} is not set:
                send "&4 informe uma profissão para criar o contrato" to player
                stop
        else:
            set {_permissao} to "%Chefe(player)%1"
            if {_permissao} is "indefinido":
                send "&l[Contratos]: &4 Você não tem permissao para fazer contratos" to player
                stop
        #PERMISSIO CHECK BLOCK CODE ==========================
        #else if player has permission "fazendeir4":
            #set {_permissao} to "fazendeiro1"
        #else if player has permission "mineradorchefe":
            #set {_permissao} to "minerador"
        #else if player has permission "escrivaochefe":
            #set {_permissao} to "escrivao"
        #else if player has permission "guardachefe":
            #set {_permissao} to "guarda"
        #else if player has permission "ferreirochefe":
            #set {_permissao} to "ferreiro"
        #else if player has permission "medicochefe":
            #set {_permissao} to "medico"
        #else if player has permission "alquimistachefe":
            #set {_permissao} to "alquimista"
        #PERMISSION CHECH BLOCK CODE ========================
        if {_tag} is 1:
            play sound "entity.villager.work_cartographer" at player
            set {_contrato} to player's held item named "&6Contrato"
            remove 1 of tool from tool
            set {_nbt} to nbt compound from {_contrato}
            set tag "Contrato" of {_nbt} to "%{_permissao}%"
            set name of {_contrato} to "&6Contrato de &5%{_permissao}%"
            give the player 1 of {_contrato}
            send "&a você cria um contrato de &5%{_permissao}%" to player
        else:
            send "%{message}%&4 você precisa de um documento vazio para fazer isso"
#=================================================================================

# ==================================== /demitir
#O demitir age de duas maneiras diferentes dependendo do seu cargo
#permissao: demitir/op
#player comum: 
# Voce deve ser um chefe de profissao para usar
# Voce informa logo depois do comando o nick do jogo da pessoa que  voce vai demitir
# A pessoa precisa estar online
# Apos esse comando a pessoa sai da lista de trabalhadores podendo usar um contrato denovo
#Admin:
# voce deve informar o nick da pessoa no primeiro parametro e no segundo qual grupo deseja tirar
# Esse comando serve quase como um atalho do luck perms
#Exemplos:
# /demitir fek_
# /demitir fek_ fazendeiro3
# /demitir fek_ werewolf
#============= testar se o chefe so consegue demitir gente do cargo dele !!!!!
#============================================== DEMITIR
command /Demitir [<player>] [<text>]:
    permission: demitir
    trigger:
        # set {_p::*} to all players where [uncolored display name of input is uncolored arg-1]
        # set {_player} to {_p::1}
        set {_player} to arg-1
        play sound "entity.villager.work_cartographer" at player
        if player has permission "admin" or "rei":
            set {_permissao} to arg-2
            if player has permission "rei":
                if {_permissao} is "admin":
                    send "&4 você é o rei mas não pode fazer isso" to player
                    stop
            wait 1 tick
            Contrato({_player},{_permissao},"remove")
            remove {_player} from {trabalhadores::*}
            send "&c você remove o jogador %{_player}% da profissão de %{_permissao}%" to player
            stop
        else:
            set {_permissao} to Chefe(player)
            send "%{_permissao}%" to player
            if {_permissao} is "indefinido":
                send "&4 você não pode demitir" to player
                stop
       #PERMISSIO CHECK BLOCK CODE ==========================
        #else if player has permission "fazendeirochefe":
            #set {_permissao} to "fazendeiro"
        #else if player has permission "mineradorchefe":
            #set {_permissao} to "minerador"
        #else if player has permission "escrivaochefe":
            #set {_permissao} to "escrivao"
       #else if player has permission "guardachefe":
            #set {_permissao} to "guarda"
        #else if player has permission "ferreirochefe":
            #set {_permissao} to "ferreiro"
        #else if player has permission "medicochefe":
            #set {_permissao} to "medico"
        #else if player has permission "alquimistachefe":
            #set {_permissao} to "alquimista"
        #PERMISSION CHECH BLOCK CODE ========================
            if arg-1 has permission {_permissao}:
                send "&4%{_permissao}% " to player
                send "&3%{_permissao}%1 " to player
                Contrato(arg-1,"{_permissao}1","remove")
                Contrato(arg-1,"{_permissao}2","remove")
                Contrato(arg-1,"{_permissao}3","remove")
                remove {_player} from {trabalhadores::*}
                send "&c você remove %{_player}'s display name% da profissão de %{_permissao}%" to player
            else:
                send "%{_message}% Voce nao pode demitir alguem que nao trabalha para voce"

#==================================================================


#======================================================== /promover
#O promover funciona de 2 formas diferentes dependendo do seu cargo
#permissao: promover
#Player comum
# Voce deve informar o nick da pessoa como primeiro parametro 
# Voce deve informar o nivel da promocao de 1 a 3 (sendo 1 praticamente inutil). Se a pessoa informar por exemplo -3 ou 5 nada ira acontecer
# A pessoa deve estar online
#Admin:
# Voce deve informar o nick da pessoa no primeiro parametro
# Voce deve informar a profissao da pessoa
# Voce deve informar o nivel da pessoa
# Esse comando praticamente pode adicionar para voce a pessoa onde voce desejar
#Exemplo:
# /promover fek_ 2
# /promover fek_ fazendeiro 3
#====================================== PROMOVER
#=========================== CHECAR SE A PESSOA SO PODE PROMVOER QUEM TRABALHA PARA ELA
command /promover [<player>] [<integer>] [<text>]:
    permission: promover
    trigger:
        if player has permission "admin":
            set {_permissao} to arg-3
            send "promovido o player %arg-1% para %arg-3%%arg-2%" to player
        else:
            set {_permissao} to Chefe(player)
            send {_permissao} to player
            if arg-1 has permission {_permissao}:
                set {_nivel} to arg-2
                if {_nivel} > 3:
                    send "você não pode fazer isso, voce so pode promover para 1 a 3" to player
                    stop
                else:
                    set {_nivel} to arg-2
                    set {_promocao} to "%{_permissao}%%{_nivel}%"
                    send "voce promove a pessoa para %{_promocao}%" to player
                    Contrato(arg-1,{_promocao},"add")
            else:
                send "Voce nao pode promover alguem que nao trabalha para voce" to player
        send {_promocao} to player
                
# =========================================


#========================= /naotrabalhador
#Esse comando serve para tirar uma pessoa da lista de trabalhadores permitindo ela usar contratos novamente
#Serve para arrumar erros e testes quando o /demitir nao funcionar por alguma razao
command /naotrabalhador [<player>]:
    permission: admin
    trigger:
        remove arg-1 from {trabalhadores::*}

#==============================================


#================================== USAR O CONTRATO
#Os contratos possuem em seu titulo o nome do grupo informado neles
#Todavia seu titulo é meramente visual, ou seja um rename nao influenciaria em nada
#As informacoes do grupo que o player sera adicionado ao usar o contrato estao na sua nbt

#========================= USAR CONTRATO
on right click:
    if player's held item is flower banner pattern:
        set {_nbt} to nbt compound from player's held item
        set {_tag} to tag "Contrato" of {_nbt}
        if {_tag} is set:
            if {trabalhadores::*} contains event-player:
                send "&4 você ja trabalha numa profissão!" to player
                stop
            Contrato(event-player,{_tag},"add")
            send "&a Você Assina o documento" to player
            #pega o item na mão
            set {_item} to player's held item
            #pega o nbt compound do item na mão
            set {_nbt} to nbt compound from {_item}
            #deleta do nbt compound a tag Contrato
            delete tag "Contrato" of {_nbt}
            #define o nome
            set {_nome} to player's display name
            set tag "Assinatura" of {_nbt} to {_nome}
            set tag "CustomModelData" of {_nbt} to 2
            remove 1 of tool from tool
            give the player 1 of {_item}
            set {_tag} to tag "Assinatura" of {_nbt}
            set lore of player's tool to "&fAssinado por:&5%{_tag}%"
            add event-player to {trabalhadores::*}
            play sound "entity.villager.work_cartographer" at player
        # broadcast "%{data}%"
        # broadcast "%{_tag}%"


command /nbtSegurando:
    permission: admin
    trigger:
        set {_nbt} to nbt compound from player's held item
        send "===================== NBT do ITEM ======================"
        send {_nbt} to player
        send "===================== NBT do ITEM ======================"